#!/usr/bin/env perl
use strict;
use warnings;

our $VERSION = '0.01';

use FindBin;
use lib "$FindBin::RealBin/../lib";
use lib "$FindBin::RealBin/../local/lib/perl5";

use List::AllUtils qw( first );

use App::JuiceCalc::Simple;

=pod

=encoding utf8

=head1 NAME

jcalc - a powerful juice calculator

=head1 VERSION

version 0.01

=cut

use Getopt::Long::Descriptive;
use Pod::Usage;
my ($opt, $usage) = describe_options(
  "%c %o",
  [ 'flavor|f=s', "the flavor file to mix" ],
  [ 'base_file|bf=s',   "the base file to mix" ],
  [ 'base|b=s',   "base override" ],
  [ 'mg|m=s',     "final mg/ml desired" ],
  [ 'nic=s',      "base override" ],
  [ 'fill|r=s',       "fill override" ],
  [ 'size|s=i',   "the amount to mix" ],
  [ 'emitter|e=s', "the emitter to use" ],
  [],
  [ 'list-emitters', "list the available emitters and exit" ],
  [ 'help|h',     "print usage and exit" ],
  [ 'man',        "print the manual page and exit" ],
  { getopt_conf => [ 'bundling_override' ] }
);
#use Data::Dump; dd [ opt => \%{ $opt } ];
print($usage->text), exit if $opt->{help};
pod2usage( -verbose => 2 ) if $opt->{man};

if ($opt->{list_emitters}) {
  my $info = list_emitters;
  for my $f (sort keys %$info) {
    print "\n";
    print "$info->{$f}{name}:\n";
    print "\t$info->{$f}{summary}\n" if $info->{$f}{summary};
    print "\t$info->{$f}{description}" if $info->{$f}{description};
    print "\n";
  }
  exit;
}

=head1 SYNOPSIS

    jcalc [-befhmrs] [long options...]
      -f --flavor         the flavor file to mix
      --bf --base_file    the base file to mix
      -b --base           base override
      -m --mg             final mg/ml desired
      --nic               base override
      -r --fill           fill override
      -s --size           the amount to mix
      -e --emitter        the emitter to use

      --list-emitters     list the available emitters and exit
      -h --help           print usage and exit
      --man               print the manual page and exit

    jcalc -f examples/fuzzy_custard.flav -b examples/juice_talk_13.base \
      -s 10 --emit Default::default_table

    jcalc -f examples/fuzzy_custard.flav

=cut

if ($opt->{flavor}) {
  die "Can't find flavor file: $opt->{flavor}\n" unless -f $opt->{flavor};
  do $opt->{flavor};
}
else {
  die "No flavor file given\n";
}

if ($opt->{base_file}) {
  die "Can't find base file: $opt->{base_file}\n" unless -f $opt->{base_file};
  do $opt->{base_file};
}

base split ',', $opt->{base} if $opt->{base};
mg $opt->{mg} if $opt->{mg};
nic split ',', $opt->{nic} if $opt->{nic};
fill split ',', $opt->{fill} if $opt->{fill};

emitter $opt->{emitter} if $opt->{emitter};
size $opt->{size} if $opt->{size};

batch_generate;

__END__

=head1 OPTIONS AND ARGUMENTS

=cut

